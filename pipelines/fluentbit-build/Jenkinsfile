@Library('polaris')
import ca.bc.gov.nrids.polaris.Podman
import ca.bc.gov.nrids.polaris.BrokerIntention
import ca.bc.gov.nrids.polaris.Vault
import ca.bc.gov.nrids.polaris.JenkinsUtil

pipeline {
    agent {
        label Podman.AGENT_LABEL_WEB
    }
    environment {
        APP_ROLE_ID = credentials('knox-vault-fluent-bit-tools-role-id')
        NR_BROKER_JWT = credentials('nr-broker-jwt')
    }
    stages {
        stage('Setup') {
            steps {
                script {
                    intention = new BrokerIntention(readJSON(file: 'scripts/intention-fb-agent-build.json'))
                    intention.setEventDetails(
                        url: env.BUILD_URL,
                        packageBuildName: "fluent-bit-rhel${params.RHEL_VERSION}",
                        packageBuildVersion: "${params.FLUENT_BIT_VERSION}+rhel${params.RHEL_VERSION}",
                        packageBuildBuildVersion: "v${params.FLUENT_BIT_VERSION}"
                    )
                    if (!intention.open(NR_BROKER_JWT, ttl: 1600)) {
                        currentBuild.result = 'ABORTED'
                        error('Intention could not be opened')
                    }
                    sh 'rm -f -r $(pwd)/dropbox'
                    sh 'mkdir $(pwd)/dropbox'
                    sh 'chmod 777 $(pwd)/dropbox'
                }
            }
        }
        stage('Get credentials') {
            steps {
                script {
                    intention.startAction("login")
                    def vaultToken = intention.provisionToken("login", APP_ROLE_ID)
                    def vault = new Vault(vaultToken)
                    def buildCreds = vault.read("apps/data/tools/fluent/fluent-bit")
                    env.ARTIFACTORY_USERNAME = buildCreds['artifactory_username']
                    env.ARTIFACTORY_PASSWORD = buildCreds['artifactory_password']
                    vault.revokeToken()
                    intention.endAction("login")
                }
            }
        }
        stage('build') {
            steps {
                script {
                    intention.startAction("build")
                    def podman = new Podman(this, null, "artifacts.developer.gov.bc.ca")
                    podman.login(options: "-u ${env.ARTIFACTORY_USERNAME} -p ${env.ARTIFACTORY_PASSWORD}")
                    podman.build(
                        options: "--build-arg FLUENT_BIT_VERSION=${params.FLUENT_BIT_VERSION} -v \$(pwd)/dropbox:/dropbox -f \$(pwd)/agent/Dockerfile-rhel${params.RHEL_VERSION}"
                    )
                    sh "ls -alh \$(pwd)/dropbox/${params.FLUENT_BIT_VERSION}"
                    def shaHash = sh(script: "sha256sum \$(pwd)/dropbox/${FLUENT_BIT_VERSION}/fluent-bit-rhel${RHEL_VERSION}.tar.gz | awk '{printf \$1}'", returnStdout: true)
                    println shaHash
                    intention.patchAction("build", "{\"package\":{\"checksum\": \"sha256:${shaHash}\"}}")
                    podman = new Podman(this, null, "releases-docker.jfrog.io/jfrog")
                    wrap([$class: 'MaskPasswordsBuildWrapper', varPasswordPairs: [[var: env.ARTIFACTORY_USERNAME, password: ARTIFACTORY_USERNAME], [var: env.ARTIFACTORY_PASSWORD, password: ARTIFACTORY_PASSWORD]]]) {
                        withEnv(["FLUENT_BIT_VERSION=${params.FLUENT_BIT_VERSION}", "RHEL_VERSION=${params.RHEL_VERSION}"]) {
                            podman.run("jfrog-cli-v2-jf:2.67.0",
                                options: "-v \$(pwd)/dropbox:/dropbox -e ARTIFACTORY_*",
                                command: 'jf rt upload "/dropbox/${FLUENT_BIT_VERSION}/fluent-bit-rhel${RHEL_VERSION}.tar.gz" \
                                    cc20-fluent-generic-local/fluent-bit/${FLUENT_BIT_VERSION}/ \
                                    --flat=true \
                                    --url=https://artifacts.developer.gov.bc.ca/artifactory \
                                    --user=${ARTIFACTORY_USERNAME} --password=${ARTIFACTORY_PASSWORD}')
                        }
                    }
                    intention.endAction("build")
                }
            }
        }
    }
    post {
        success {
            script {
                intention.close(true)
            }
        }
        unstable {
            script {
                if (intention) {
                    intention.close(false)
                }
            }
        }
        failure {
            script {
                if (intention) {
                    intention.close(false)
                }
            }
        }
    }
}