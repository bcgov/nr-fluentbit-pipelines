@Library('polaris')
import ca.bc.gov.nrids.polaris.Podman
import ca.bc.gov.nrids.polaris.BrokerIntention
import ca.bc.gov.nrids.polaris.Vault
import ca.bc.gov.nrids.polaris.JenkinsUtil

def podman
def intention
def commonModule = [:]

pipeline {
    agent {
        label Podman.AGENT_LABEL_APP
    }
    environment {
        FB_RUN_USER="wwwsvr"
        FB_S6_SERVICE_HOME = "/apps_ux/s6_services"
        FB_FLUENTBIT_DB = "/apps_data/agents/fluent-bit/fluent-bit-logs.db"
        FB_TAIL_FILES_LIKE = "${params.fluentbitFilesLike}"
        FB_BROKER_URL = "https://broker.io.nrs.gov.bc.ca"
        FB_CONFIG_ROLE_ID = credentials('knox-jenkins-jenkins-apps-prod-role-id')
        NR_BROKER_JWT = credentials('nr-broker-jwt')
        FB_BUILD_URL = "${env.BUILD_URL}"
        IMAGE_TAG = "${params.imageTag}"
        AUTHFILE = "fluent-bit.auth.json"
    }
    stages {
        stage('Setup') {
            environment {
                HTTP_PROXY = "http://test-forwardproxy.nrs.bcgov:23128"
                FB_GIT_BRANCH = "${params.fbGitBranch}"
            }
            steps {
                script {
                    sh 'rm -rf fb && mkdir fb'
                    sh "git clone -b ${params.fbGitBranch} https://github.com/bcgov/nr-funbucks.git fb"
                    commonModule = load "pipelines/common.groovy"
                    intention = new BrokerIntention(NR_BROKER_JWT, readJSON(file: 'scripts/intention-fb-configure.json'))
                    intention.setEventDetails(
                        userName: JenkinsUtil.getCauseUserId(currentBuild),
                        url: env.BUILD_URL,
                        reason: "Reload logs"
                    )
                    intention.open()
                    intention.startAction("login")
                    def vaultToken = intention.provisionToken("login", FB_CONFIG_ROLE_ID)
                    def vault = new Vault(vaultToken)
                    def registryCreds = vault.read('apps/data/prod/jenkins/jenkins-apps/artifactory')
                    env.REGISTRY_USERNAME = registryCreds['REGISTRY_USERNAME']
                    env.REGISTRY_PASSWORD = registryCreds['REGISTRY_PASSWORD']
                    env.FB_HOST = commonModule.getHost("${params.fluentbitHost}")
                    env.FB_SERVER_OS = commonModule.getServerOS("${params.fluentbitHost}")
                    if (env.FB_SERVER_OS != "linux") {
                        currentBuild.result = 'ABORTED'
                        error('Invalid server OS')
                    }
                    env.FB_BIN_DIR = "${env.FB_SERVER_OS == "linux" ? "/sw_ux/bin" : "E:/sw_nt"}"
                    env.FB_AGENT_ROOT = "${env.FB_SERVER_OS == "linux" ? "/apps_ux/agents" : "E:/apps_nt/agents"}"
                    env.FB_VAULT_CD_USER_FIELD = commonModule.getVaultCdUserField("${params.fluentbitHost}")
                    env.FB_VAULT_CD_PASS_FIELD = commonModule.getVaultCdPassField("${params.fluentbitHost}")
                    env.FB_VAULT_CD_PATH = commonModule.getVaultCdPath("${params.fluentbitHost}")
                    def vault_cd_path = "${env.FB_VAULT_CD_PATH}"
                    def cdCreds = vault.read(vault_cd_path.replaceFirst('/', '/data/'))
                    env.FB_CD_USER = cdCreds[env.FB_VAULT_CD_USER_FIELD]
                    env.FB_CD_PASS = cdCreds[env.FB_VAULT_CD_PASS_FIELD]
                    vault.revokeToken()
                }
            }
        }
        stage('Reload logs') {
            steps {
                script {
                    podman = new Podman(this, null, null, env.AUTHFILE, "artifacts.developer.gov.bc.ca/cc20-gen-docker-local")
                    podman.login(authfile: "${env.AUTHFILE}", options: "-u ${env.REGISTRY_USERNAME} -p ${env.REGISTRY_PASSWORD}")
                    intention.startAction("configure")
                    podman.run("fb-helper:${params.imageTag}",
                        authfile: "${env.AUTHFILE}",
                        options: "-v \$(pwd)/files/ssh-config:/app/ssh-config -v \$(pwd)/scripts:/app/scripts -v /sw_ux/bin:/sw_ux/bin -e 'FB_*' -e 'VAULT_*'",
                        command: "scripts/${FB_SERVER_OS.toLowerCase()}/reload_logs.sh")
                    intention.endAction("configure")
                    podman.logout(authfile: "${env.AUTHFILE}")
                    intention.endAction("login")
                }
            }
        }
    }
    post {
        success {
            script {
                intention.close(true)
            }
        }
        unstable {
            script {
                if (intention) {
                    intention.close(false)
                }
            }
        }
        failure {
            script {
                if (intention) {
                    intention.close(false)
                }
            }
        }
    }
}
